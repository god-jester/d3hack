function(exl_load_config_mk)
    set(options)
    set(oneValueArgs PATH)
    set(multiValueArgs VARS)
    cmake_parse_arguments(EXL_CFG "" "PATH" "VARS" ${ARGN})

    if(NOT EXL_CFG_PATH)
        message(FATAL_ERROR "exl_load_config_mk requires PATH")
    endif()

    if(NOT EXISTS "${EXL_CFG_PATH}")
        return()
    endif()

    file(STRINGS "${EXL_CFG_PATH}" EXL_CFG_LINES)
    foreach(line IN LISTS EXL_CFG_LINES)
        string(REGEX REPLACE "#.*$" "" line "${line}")
        string(STRIP "${line}" line)
        if(line STREQUAL "")
            continue()
        endif()
        if(line MATCHES "^([A-Za-z0-9_]+)[ \t]*:?=[ \t]*(.*)$")
            set(var "${CMAKE_MATCH_1}")
            set(val "${CMAKE_MATCH_2}")
            string(STRIP "${val}" val)

            if(EXL_CFG_VARS)
                list(FIND EXL_CFG_VARS "${var}" var_index)
                if(var_index EQUAL -1)
                    continue()
                endif()
            endif()

            if(NOT DEFINED ${var})
                set(${var} "${val}" PARENT_SCOPE)
            endif()
        endif()
    endforeach()
endfunction()
